% basic helpers/language tweaks for nial
% --------------------------------------------------;
loaddefs "math;

ok := null;

% renames and abbreviations;
id is pass;
ph is phrase;
head is first;
init is front;
tail is rest;
ord is charrep;
chr is char;
zip is eachboth link;
ints is tell;

% string tools;
words is op Str {
  % split string on space character;
  % (from towords.ndf in nial distro);
  Strings := EACH phrase ( `  match Str cut Str ) }

unquote is op q {
  % convert !( quoted expressions ) back to text;
  link descan scan link descan deparse q };

% glue a string together;
implode is op glue parts {
  link[head, link(glue eachright link tail)] parts }


% more helpers...;

% does any item in given array match predicate p?
% ex:  !(any atomic a b c d etc) -> are any of them atomic?;
any is tr p ( or EACH p);

% do all items in given array match predicate p?
% ex:  !( all isboolean ) <-> !( allbools );
all is tr p ( and EACH p);

% implication, like short circuiting 'and';
imp is tr f g op x (
  if f x then g x else o endif );


% remove nesting until a simple or non-solitary array;
% mostly to allow using [...] with fewer commas ;
unboxed is fork[and[1=tally, not simple],unboxed head,id];

% -- basic boolean operations ----------------------;

nor is and (each not);
atop is tr f g op x y { f (x g y) };
always is eachboth (op x y { l });
never is eachboth (op x y { o });

xor is op x y {
  % bitwise xor;
  (x and not y) or (y and not x) }


% array reshaping (generalized string tools)
% -------------------------------------------;

rpad is op ary newlen pad {
  % pad array on right to reach new length, if necessary;
  if (len := tally ary)  < newlen then
    ary link (newlen - len reshape pad)
  else ary endif }

lpad is op ary newlen pad {
  % pad array on left to reach new length, if necessary;
  if (len := tally ary)  < newlen then
    newlen - len reshape pad link ary
  else ary endif };

rechunk is op ar sz {
  % chop array into chunks of given size;
  if ( tally ar ismulof sz ) then
    result := rows ([tally ar idv sz, sz] reshape ar)
  else fault 'array length must be multiple of chunk size' endif }

lfit is op ary newlen pad {
  % pad or truncate array to fit length;
  if (len := tally ary) > newlen then
    (len - newlen) drop ary
  else lpad ary newlen pad endif }

